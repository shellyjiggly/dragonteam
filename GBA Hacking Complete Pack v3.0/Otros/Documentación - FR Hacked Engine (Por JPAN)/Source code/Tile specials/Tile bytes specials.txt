.thumb
.align 2

/*Special 0x7e, Tile Number getter, gets the tile's number at (0x8004, 0x8005)
location. Should usually be used in conjunction with special 0x8f, the player 
location to variables special. Returns to a given variable the desired number*/

Special_7e:	push {r1-r4, lr}
		ldr r1, var_8004
		ldrh r0, [r1]
		ldrh r1, [r1, #0x2]
		add r1, #0x7	/*long story, but it's mainly because that's*/
		add r0, #0x7	/*how the game makes the access, anyway*/
		mov r2, #0x0
		bl call_tile_getter
		pop {r1-r4, pc}

var_8004: .word 0x020370c0
call_tile_getter:	ldr r3, getter
			bx r3
getter: .word 0x08058E49		/*this function returns the tile number of a 
				given position on the current map*/
.word 0x00000000
/*special 0x7f is the Tile attribute getter. It returns to var 0x8004 the value
of that tile behaviour bytes and 0x8005 is the tile's back bytes. Designed to
be used with special 0x8f before it (to get the positions) and to be possible to
use the required wild battle randomizer afterwards.
0x8004 will then keep the background byte and 0x8005 will keep the upper bytes
will return to a given variable the background bytes */

Special_7f:	push {r1-r4, lr}
		ldr r4, var_8004_2
		ldrh r0, [r4]
		ldrh r1, [r4, #0x2]
		add r1, #0x7	/*long story, but it's mainly because that's*/
		add r0, #0x7	/*how the game makes the access, anyway*/
		mov r2, #0xFF
		bl call_tile_evaluator
		lsl r1, r0, #0x10 /*the behaviour bytes*/
		lsr r0, r0, #0x18 /*the background byte*/
		lsr r1, r1, #0x10
		strh r0,[r4,#0x0]
		strh r1,[r4,#0x2]
		mov r1, #0x3
		and r0, r1
		pop {r1-r4, pc}

call_tile_evaluator:	ldr r3, evaluator
			bx r3
.hword 0x0000
evaluator:	.word 0x08058F49
var_8004_2: .word 0x020370c0
.word 0x00000000

/*Special 0x97 will start a random wild battle, such as those in the grass. Made
to reverse the shortcomming of the "Walking script hack", that disables wild 
battles on land and at sea. This special deals with land battles*/

Special_97: push {r4-r7, lr}
	    mov r7, r8
	    push {r7}
	    sub SP, SP, #0x8
	    ldr r0, grass_data
	    lsl r4, r0, #0x18
	    lsr r4, r4, #0x18
	    ldr r1, wild_generator
	    bx r1
.hword 0x0000
grass_data: .word 0x01000202 /*a normal grass block data. any data would do*/
wild_generator: .word 0x0806cbe5

.word 0x00000000

/*Special 0x98 will start a random water battle, such as those in the sea.
Sister to the previous function, will start a water battle anywhere there is 
a water pokemon data.*/

Special_98: push {r4-r7, lr}
	    mov r7, r8
	    push {r7}
	    sub SP, SP, #0x8
	    ldr r0, water_data
	    lsl r4, r0, #0x18
	    lsr r4, r4, #0x18
	    ldr r1, wild_generator2
	    bx r1
.hword 0x0000
water_data: .word 0x22000410 /*the lake tile data*/
wild_generator2: .word 0x0806cbe5

.word 0x00000000

/*Special 0x81 is the loader for the Walking script hack. just load the pointer
with the loadPointer command and this special will take care of the rest.*/

Special_81: 	ldr r0, load_pointer
		ldr r1, store_location
		ldr r0, [r0]
		str r0, [r1]
		mov r0, lr
		bx r0
		
load_pointer: .word 0x03000f14
store_location: .word 0x0203f4f0
